load(
    "//java/kotlin-extractor:versions.bzl",
    "VERSIONS",
    "get_compatilibity_sources",
    "version_less",
)
load("@rules_kotlin//kotlin:jvm.bzl", "kt_jvm_library")
load("@rules_kotlin//kotlin:core.bzl", "kt_kotlinc_options")

py_binary(
    name = "generate_dbscheme",
    srcs = ["generate_dbscheme.py"],
)

genrule(
    name = "generated-dbscheme",
    srcs = ["//java:dbscheme"],
    outs = ["KotlinExtractorDbScheme.kt"],
    cmd = "$(execpath :generate_dbscheme) $< $@",
    tools = [":generate_dbscheme"],
)

_resources = [
    (
        r,
        r[len("src/main/resources/"):],
    )
    for r in glob(["src/main/resources/**"])
]

[
    (
        kt_kotlinc_options(
            name = "kotlinc-options-%s" % v,
            include_stdlibs = "none",
            jvm_target = "1.8",
            language_version = v[:3],
            warn = "error",
            x_optin = [
                "kotlin.RequiresOptIn",
                "org.jetbrains.kotlin.ir.symbols.%s" %
                ("IrSymbolInternals" if version_less(v, "2.0.0") else "UnsafeDuringIrConstructionAPI"),
            ],
            x_suppress_version_warnings = True,
        ),
        # * extractor.name is different for each version, so we need to put it in different output dirs
        # * in order to put it in `resources`, we need to define `resource_strip_prefix` to strip this version
        # * `resource_strip_prefix` is unique per jar, so we must also put other resources under the same version prefix
        genrule(
            name = "resources-%s" % v,
            srcs = [src for src, _ in _resources],
            outs = [
                "%s/com/github/codeql/extractor.name" % v,
            ] + [
                "%s/%s" % (v, tgt)
                for _, tgt in _resources
            ],
            cmd = "\n".join([
                "echo codeql-extractor-kotlin-standalone-%s > $(RULEDIR)/%s/com/github/codeql/extractor.name" % (v, v),
            ] + [
                "cp $(execpath %s) $(RULEDIR)/%s/%s" % (src, v, tgt)
                for src, tgt in _resources
            ]),
        ),
        kt_jvm_library(
            name = "codeql-extractor-kotlin-standalone-%s" % v,
            srcs =
                [":generated-dbscheme"] +
                glob(
                    [
                        "src/**/*.kt",
                        "src/**/*.java",
                    ],
                    exclude = ["src/main/kotlin/utils/versions/**"],
                ) + get_compatilibity_sources(v, "src/main/kotlin/utils/versions"),
            kotlinc_opts = ":kotlinc-options-%s" % v,
            module_name = "codeql-kotlin-extractor",
            resource_strip_prefix = "%s/%s" % (
                package_name(),
                v,
            ),
            resources = [
                ":resources-%s" % v,
            ],
            deps = [
                "@kotlin-compiler-%s" % v,
                "@kotlin-stdlib-%s" % v,
            ],
        ),
    )
    for v in VERSIONS
]
